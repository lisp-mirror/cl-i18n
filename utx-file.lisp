;; This software is Copyright (c) cage, 2012.
;; cage grants you the rights to distribute
;; and use this software as governed by the terms
;; of the Lisp Lesser GNU Public License
;; (http://opensource.franz.com/preamble.html),
;; known as the LLGPL

(in-package :utx-file)

(alexandria:define-constant +column+ "[^\\t\\n]+(\\t|\\n)" :test 'equalp)
(alexandria:define-constant +column-id+ "[0-9]+\\t" :test 'equalp)
(alexandria:define-constant +utx-ignored-line+ "(#.*\\r\\n)|(^\\p{white_space}+\\r\\n)|(^\\r\\n)" :test 'string=)
(alexandria:define-constant +line-stopper+ #\NewLine :test 'char=)
(alexandria:define-constant +status-forbidden+ "forbidden" :test 'string=)
(alexandria:define-constant +plural-field+ "src:plural" :test 'string=)
(alexandria:define-constant +status-field+ "term status" :test 'string=)


(defparameter *fields-position* (make-hash-table :test 'equal))

(defclass utx-parsed-file (cl-i18n:parsed-file) ())

(defgeneric last-comment-line-p (object))

(defmethod initialize-instance :after ((object utx-parsed-file) &key &allow-other-keys)
  (with-slots (comment-line) object
    (setf utx-file:comment-line +utx-ignored-line+)))


(defmethod peek-token ((object utx-parsed-file) &optional (test #'identity))
  (if (cl-i18n:peek-valid-stream)
      (multiple-value-bind (token start-token)
	  (cl-i18n:next-token object)
	(prog1
	    (funcall test token)
	  (cl-i18n:seek cl-i18n:*file* start-token)))
      nil))

(defmethod last-comment-line-p ((object utx-parsed-file))
  (cl-i18n:with-no-errors
    (multiple-value-bind (line length start)
	(cl-i18n:get-line cl-i18n:*file*)
      (declare (ignore length))
    (unwind-protect
	 (not (cl-i18n:is-comment-line-p object line))
      (cl-i18n:seek cl-i18n:*file* start)))))

(defmethod parse-comment-line ((object utx-parsed-file))
  (cl-i18n:with-no-errors
    (multiple-value-bind (line length start)
	(cl-i18n:get-line cl-i18n:*file*)
      (declare (ignore length))
      (if (and
	   (cl-i18n:is-comment-line-p object line)
	   (not (last-comment-line-p object)))
	  (progn
	    (parse-comment-line object))
	  (progn
	    (cl-i18n:seek cl-i18n:*file* start)
	    nil)))))
	
    


(cl-i18n:define-parser-skeleton utx utx-parsed-file 
   (*fields-position* (make-hash-table :test 'equal)))


(cl-i18n:define-tokenizer (utx-file:utx-parsed-file +column+))

(defun last-column-p (col)
  (char= (char col (1- (length col))) +line-stopper+))


(defun row-src (row)
  (first row))

(defun row-target (row)
  (second row))

(defmacro get-field (key row)
  (alexandria:with-gensyms (pos)
    `(let ((,pos (gethash ,key *fields-position*)))
       (if ,pos
	   (nth ,pos ,row)
	   nil))))

    
(defun row-status (row)
  (get-field +status-field+ row))

(defun row-plural (row)
  (get-field +plural-field+ row))

(defun status-forbidden-p (row)
  (string= +status-forbidden+ (row-status row)))


(cl-i18n:defnocfun parse-utx-file ()
  (cl-i18n:with-no-errors
    (if (cl-i18n:peek-valid-stream)
	(progn
	  (parse-utx-column-description)
	  (values (parse-utx-lines)
		  #'cl-i18n:english-plural-form 
		  cl-i18n:*has-errors* 
		  cl-i18n:*parsing-errors*))
	(values nil
		#'cl-i18n:english-plural-form 
		cl-i18n:*has-errors* 
		cl-i18n:*parsing-errors*))))


(defun parse-utx-column-description ()
  (let ((fields (trim-rows (parse-utx-line))))
    (loop for i from 0 below (length fields) do
	 (setf (gethash (nth i fields) *fields-position*) i))))


(defun min-column-number-p (row)
  (if (and row
	   (< (length row) 3))
      (progn
	(setf cl-i18n:*has-errors* t)
	(push (format nil "Error: utx row has less than 3 field: 簋蝻鳗沆楸割邯疳蝮轭绛弪蝻蝮铋飑蝻鳗ㄤ彐躅趄轫蝻黧蝻黧镳糸镱犰ㄢ徵ㄦ矧磲铋狺狺幄＼葬＼五黛轭＼义趱蝾┅磲疸狎＇灬礅溽ㄣ篝蜷铉趄轫忉悌蝻黧┅ㄤ彐躅疳蝮瀛豸扉铄é镳糸镱犰ㄣ躜蝈铘扉铄Ж┅ㄥ铘蜷弩磲脲栳箬翎忪呼弩у聃犰┅ㄤ邈灬蝈镳糸黹箴邋畅筢驽豉癌ㄤ邂蹒癌┅灬忮祗è珏舡蝻īㄣ飙楸割瑚轸璀铒弪蝻蝮戾è蝻疳蝮瀛豸扉铄┅ㄩ黹瞽泔祯眍铛礅弪蝻鳗趄轫蝻黧蝻鳗铋飑┅┅＋筲沆ㄩㄣ飙楸割吼邋氕鲠扉洵篝蝈犴痱镧箦翩沲蝌孱舡扉铄ㄧ弭蝻鳗麒孱沲蝌孱舡扉铄箦翩ㄧ弭栳箬蝻鳝篁沲蝌孱舡扉铄孱趄殄螬ㄣ飙楸割喉犭瀛趄犷箪狒轱蝻鳝翎蜱弭沲蝌孱舡扉铄沆楸割韩趄犷箪狒邃骒徵扉篝蝻鳝痨躜犰沲蝌孱舡扉铄┅┅疳蝮瀛豸扉铄沲蝌孱舡扉铄孱趄殄螬孱趄殄螬－筲沆ㄤè蝻ㄧ弭蝻鳗ㄧ弭蝻鳗┅è铒ㄣ飙楸割吼邋氕鲠扉洵篝蝈犴┅孱趄殄螬箦翩ㄧ弭栳箬蝻鳝篁蝻鳗孱趄殄螬ㄣ飙楸割喉犭瀛趄犷箪狒轱蝻鳝翎蜱弭蝻鳗沆楸割韩趄犷箪狒邃骒徵扉篝蝻鳝痨躜犰蝻鳗┅┅┅ㄤ彐躅疳蝮瀛豸扉铄é脲祜镫骘颦泔眄孱舂麒孱ㄡ钿祜镫骘颦泔眄孱ㄣ飙楸割吼邋氕鲠扉洵篝蝈犴┅疳蝮瀛泔眄孱舡扉铄沆楸割邯骈戾┅ㄣ飙楸割瑚轸璀铒弪蝻蝮ㄩㄣ飙楸割吼邋氕鲠扉洵篝蝈犴戾è泔疳蝮瀛豸泔祯眍┅ㄩ泔ㄩ铒灬篝泔祯眍泔飑ㄡ痧孱扉篝泔飑疳蝮瀛豸扉铄红镲氕骘颦泔眄孱铋飑扉篝泔飑┅铋飑┅ㄤ彐躅疳蝮瀛豸泔祯眍īㄣ飙楸割瑚轸璀铒弪蝻蝮ㄩㄣ飙楸割吼邋氕鲠扉洵篝蝈犴ㄣ飙楸割侯屮舡麸脲沆楸割邯骈戾铋飑┅